apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
android {
    libraryVariants.configureEach { variant ->
        if (variant.name == 'release') {
            tasks.register('javadoc', Javadoc) {
                source = android.sourceSets.main.java.srcDirs
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
                owner.classpath += variant.javaCompileProvider.get().classpath
                exclude '**/R.html', '**/R.*.html', '**/index.html'
            }

            // build a jar with javadoc
            tasks.register('javadocJar', Jar) {
                dependsOn javadoc
                archiveClassifier.set('javadoc')
                from javadoc.destinationDir
            }

            // build a jar with source
            tasks.register('sourcesJar', Jar) {
                archiveClassifier.set('sources')
                from android.sourceSets.main.java.srcDirs
            }
        }
    }
}


afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                artifact javadocJar
                artifact sourcesJar

                groupId = rootProject.group
                artifactId = project.name
                version = project.version

                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', project.ext.pomName)
                    root.appendNode('description', project.description)
                    root.appendNode('url', 'https://github.com/Yubico/yubikit-android')
                    root.appendNode('licenses').appendNode('license').with {
                        appendNode('name', 'The Apache Software License, Version 2.0')
                        appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
                        appendNode('distribution', 'repo')
                    }
                    root.appendNode('developers').appendNode('developer').with {
                        appendNode('id', 'yubico')
                        appendNode('name', 'Yubico')
                        appendNode('email', 'dev@yubico.com')
                    }
                    root.appendNode('scm').with {
                        appendNode('connection', 'scm:git:git://github.com/Yubico/yubikit-android.git')
                        appendNode('developerConnection', 'scm:git:ssh://github.com/Yubico/yubikit-android.git')
                        appendNode('url', 'https://github.com/Yubico/yubikit-android')
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'sonatype'
                url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("sonatype.username")
                    password = findProperty("sonatype.password")
                }
            }
        }
    }
    tasks.withType(Sign).configureEach {
        onlyIf { isReleaseVersion && System.getenv("NO_GPG_SIGN") != "true" }
    }
    signing {
        useGpgCmd()
        sign publishing.publications.maven
    }
}
