import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'com.github.spotbugs'
}

dependencies {
    spotbugs 'com.github.spotbugs:spotbugs:4.8.6'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'

    implementation 'com.github.spotbugs:spotbugs-annotations:4.8.6'
}

spotbugs {
    ignoreFailures = true
    showStackTraces = false
    showProgress = false

    effort = Effort.MORE
    reportLevel = Confidence.valueOf('DEFAULT')
    excludeFilter = file("../spotbugs/excludeFilter.xml")
}

tasks.withType(SpotBugsTask) {
    enabled = name.toLowerCase().contains('main') || name.toLowerCase().contains('release')

    reports {
        html {
            required = true
            outputLocation = file("${project.rootDir}/build/spotbugs-html/spotbugs-${project.name}.html")
        }
        sarif {
            required = true
            outputLocation = file("${project.rootDir}/build/spotbugs/spotbugs-${project.name}.sarif")
        }
    }
}
